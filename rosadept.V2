.PROGRAM playground()

	$a = "movehome"
	$b = "moveinterm"
	$c = "move2feeder"
	$br = "break"

	CALL skillexec($c)
	CALL skillexec($br)
	CALL skillexec($b)
	CALL skillexec($br)
	CALL skillexec($a)
	CALL skillexec($br)

.END

.PROGRAM disable_air()

	IF SIG(8) THEN
	    SIGNAL -8
	END

.END

.PROGRAM enable_air()

	IF SIG(8) == FALSE THEN
	    SIGNAL 8
	END
.END

.PROGRAM decodeskill($msg, $func, params[], nparams)

	$func = $DECODE($msg,":",0)
	$tmp = $DECODE($msg,":",1)

	IF $tmp == "" THEN
	    nparams = 0
	ELSE

	    i = 0
	    nparams = 1
	    DO
		$tmp = $DECODE($msg,",",0)
		params[i] = VAL($tmp)
		nparams = nparams+1
		$tmp = $DECODE($msg,",",1)
		i = i+1

	    UNTIL $msg == ""

	END


.END

.PROGRAM serverloop()

	AUTO do_wait, no_wait, handle, lun, repeat_loop

	do_wait = 0
	no_wait = 1

	ATTACH (lun, 4) "TCP"
	CALL typeiostat("establishing TCP server LUN", lun)

	FOPEN (lun, 16) "/LOCAL_PORT 1234 /CLIENTS 10 /BUFFER_SIZE 1024"
	CALL typeiostat("creating server socket", lun)

	repeat_loop = TRUE
	WHILE repeat_loop DO

	    WAIT
	    READ (lun, handle, do_wait) $input
	    status = IOSTAT(lun)
	    CASE status OF
	      VALUE 100:
		TYPE "Connection established"
	      VALUE 1:
		TYPE "Received message ", $input

		CALLS $input()

	      VALUE 101:
		TYPE "Connection closed"
	    END

	END


	FCLOSE (lun)
	DETACH (lun)
	CALL typeiostat("closing", lun)


.END

.PROGRAM defvars()

	; Transformations and precision points
	SET #orig = #PPOINT(0,-90,180,0,90,0)
	SET #intermediate = #PPOINT(-100,-90,180,0,90,0)
	SET #close_to_feeder = #PPOINT(-100,-54,185,0,50,0)
	SET over_griphome_1 = TRANS(167.9,-243.3,600,0,180,-90)	; to be further refined
	SET #over_griphome_2 = #PPOINT(-55.4,-121.9,219.6,0,90,-90)
	SET test_1 = TRANS(480,0,500,0,180,-90)

	; IP addresses
	$ip_vbox = "172.16.0.125"

.END

.PROGRAM gripper_close()

	CALL enable_air()
	;IF SIG(-99) AND SIG(100) THEN
	;SIGNAL 99, -100
	;END

	SIGNAL 99, -100

.END

.PROGRAM gripper_open()

	CALL enable_air()

	;TYPE SIG(99), SIG(-100)

	;IF SIG(99) AND SIG(-100) THEN
	;SIGNAL -99, 100
	;END

	SIGNAL -99, 100

.END

.PROGRAM writecsvline(lun, arr[])

	n = LAST(arr[])
	FOR i = 0 TO n-1
	    WRITE (lun) arr[i], /S
	    WRITE (lun) ",", /S
	END
	WRITE (lun) arr[n]

	TYPE "Done writing"
.END

.PROGRAM typeiostat($op_string, lun)

	err_code = IOSTAT(lun)
	TYPE "After ", $op_string, ":", err_code
	IF err_code < 0 THEN
	    TYPE $ERROR(err_code)
	END

.END

.PROGRAM tcpsend($ip, port, $msg)
	AUTO lun

	ATTACH (lun, 4) "TCP"
	FOPEN (lun, 0) $ip, " /REMOTE_PORT", port, " /BUFFER_SIZE 1024"

	WRITE (lun) $msg

	FCLOSE (lun)
	DETACH (lun)
.END

.PROGRAM tcpcreateclient($ip, port, lun)
	ATTACH (lun, 4) "TCP"
	;CALL typeiostat("establishing TCP client LUN", lun)

	IF IOSTAT(lun) < 0 THEN
	    CALL tcpdestrclient(lun)
	END

	FOPEN (lun, 0) $ip, " /REMOTE_PORT", port, " /BUFFER_SIZE 1024"
	;CALL typeiostat("creating client socket", lun)

	IF IOSTAT(lun) < 0 THEN
	    CALL tcpdestrclient(lun)
	END
.END

.PROGRAM tcpdestrclient(lun)
	FCLOSE (lun)
	DETACH (lun)
	CALL typeiostat("destroying TCP client", lun)
.END

.PROGRAM move_relative(params[])

	HERE now

	x = params[0]
	y = params[1]
	z = params[2]
	roll = params[3]
	pitch = params[4]
	yaw = params[5]

	MOVE now:TRANS(x,y,z,roll,pitch,yaw)

.END

.PROGRAM movehome()
	CALL defvars()
	MOVE #orig
.END

.PROGRAM skillexec($skill)

	CALL decodeskill($skill+"", $func, p[], nparams)

	IF nparams == 0 THEN
	    CALLS $func()
	ELSE
	    CALLS $func(p[])
	END

.END

.PROGRAM moveinterm()
	CALL defvars()
	MOVE #intermediate
.END

.PROGRAM move2feeder()
	CALL defvars()
	MOVE #close_to_feeder
.END

.PROGRAM break()
	BREAK
.END

.LOCATIONS
#close_to_feeder  -100 -54 185 0 50 0
#intermediate  -100 -90 180 0 90 0
 now         -1 -2.43559425E-05 6.79499681E-06 -2.43559498E-05 1 -1.1993726E-06
 -6.7949677E-06 -1.19953802E-06 -1 479.999237061 -4.78624686E-04 709.999694824
#orig        0 -90 180 0 90 0
 over_griphome_1  0 -1 0 -1 0 0
 0 0 -1 167.899993896 -243.300003052 600
#over_griphome_2  -55.400001526 -121.900001526 219.600006104 0 90 -90
 test_1      0 -1 0 -1 0 0
 0 0 -1 480 0 500
.END

.REALS
 err_code   1
 i          6
 nparams    0
 p[0]       0
 p[1]       10
 p[2]       5
 p[3]       0
 p[4]       20
 p[5]       0
 pitch      20
 roll       0
 status     101
 x          0
 y          10
 yaw        0
 z          5
.END

.DOUBLES
.END

.STRINGS
$a          "movehome"
$b          "moveinterm"
$br         "break"
$c          "move2feeder"
$func       "break"
$input      ""
$ip_vbox    "172.16.0.125"
$tmp        ""
.END

