.PROGRAM rosadept()
.END

.PROGRAM disable_air()

	IF SIG(8) THEN
	    SIGNAL -8
	END

.END

.PROGRAM enable_air()

	IF SIG(8) == FALSE THEN
	    SIGNAL 8
	END
.END

.PROGRAM decodeskill($msg, $func, params[], nparams)

	$func = $DECODE($msg,":",0)
	$tmp = $DECODE($msg,":",1)

	IF $tmp == "" THEN
	    nparams = 0
	ELSE

	    i = 0
	    nparams = 1
	    DO
		$tmp = $DECODE($msg,",",0)
		params[i] = VAL($tmp)
		nparams = nparams+1
		$tmp = $DECODE($msg,",",1)
		i = i+1

	    UNTIL $msg == ""

	END


.END

.PROGRAM serverloop()

	AUTO do_wait, no_wait, handle, lun, repeat_loop

	do_wait = 0
	no_wait = 1

	ATTACH (lun, 4) "TCP"
	CALL typeiostat("establishing TCP server LUN", lun)

	FOPEN (lun, 16) "/LOCAL_PORT 1234 /CLIENTS 10 /BUFFER_SIZE 1024"
	CALL typeiostat("creating server socket", lun)

	repeat_loop = TRUE
	WHILE repeat_loop DO

	    WAIT
	    READ (lun, handle, do_wait) $input
	    status = IOSTAT(lun)
	    CASE status OF
	      VALUE 100:
		TYPE "Connection established"
	      VALUE 1:
		TYPE "Received message ", $input

		CALL skillexec($input)

	      VALUE 101:
		TYPE "Connection closed"
	    END

	END


	FCLOSE (lun)
	DETACH (lun)
	CALL typeiostat("closing", lun)


.END

.PROGRAM defvars()

	ATTACH ()

	; Transformations and precision points
	SET #orig = #PPOINT(0,-90,180,0,90,0)
	SET #intermediate = #PPOINT(-100,-90,180,0,90,0)
	SET #close_to_feeder = #PPOINT(-100,-54,185,0,50,0)
	SET insp_approach = TRANS(558.356,-365.489,443.313,36.215,175.322,123.243)

	; test 
	SET #over_griphome_2 = #PPOINT(-55.4,-121.9,219.6,0,90,-90)
	SET over_griphome_1 = TRANS(167.9,-243.3,600,0,180,-90)	; to be further refined
	SET test_1 = TRANS(480,0,500,0,180,-90)

	; IP addresses
	$ip_vbox = "172.16.0.125"
.END

.PROGRAM gripper_close()

	CALL enable_air()
	;IF SIG(-99) AND SIG(100) THEN
	;SIGNAL 99, -100
	;END

	SIGNAL 99, -100

.END

.PROGRAM gripper_open()

	CALL enable_air()

	;TYPE SIG(99), SIG(-100)

	;IF SIG(99) AND SIG(-100) THEN
	;SIGNAL -99, 100
	;END

	SIGNAL -99, 100

.END

.PROGRAM writecsvline(lun, arr[])

	n = LAST(arr[])
	FOR i = 0 TO n-1
	    WRITE (lun) arr[i], /S
	    WRITE (lun) ",", /S
	END
	WRITE (lun) arr[n]

	TYPE "Done writing"
.END

.PROGRAM typeiostat($op_string, lun)

	err_code = IOSTAT(lun)
	TYPE "After ", $op_string, ":", err_code
	IF err_code < 0 THEN
	    TYPE $ERROR(err_code)
	END

.END

.PROGRAM tcpsend($ip, port, $msg)
	AUTO lun

	ATTACH (lun, 4) "TCP"
	FOPEN (lun, 0) $ip, " /REMOTE_PORT", port, " /BUFFER_SIZE 1024"

	WRITE (lun) $msg

	FCLOSE (lun)
	DETACH (lun)
.END

.PROGRAM tcpcreateclient($ip, port, lun)
	ATTACH (lun, 4) "TCP"
	;CALL typeiostat("establishing TCP client LUN", lun)

	IF IOSTAT(lun) < 0 THEN
	    CALL tcpdestrclient(lun)
	END

	FOPEN (lun, 0) $ip, " /REMOTE_PORT", port, " /BUFFER_SIZE 1024"
	;CALL typeiostat("creating client socket", lun)

	IF IOSTAT(lun) < 0 THEN
	    CALL tcpdestrclient(lun)
	END
.END

.PROGRAM tcpdestrclient(lun)
	FCLOSE (lun)
	DETACH (lun)
	CALL typeiostat("destroying TCP client", lun)
.END

.PROGRAM move_relative(params[])

	HERE now

	x = params[0]
	y = params[1]
	z = params[2]
	roll = params[3]
	pitch = params[4]
	yaw = params[5]

	MOVE now:TRANS(x,y,z,roll,pitch,yaw)

.END

.PROGRAM movehome()
	CALL defvars()
	MOVE #orig
.END

.PROGRAM skillexec($skill)

	CALL decodeskill($skill+"", $func, p[], nparams)

	IF nparams == 0 THEN
	    CALLS $func()
	ELSE
	    CALLS $func(p[])
	END

.END

.PROGRAM moveinterm()
	CALL defvars()
	MOVE #intermediate
.END

.PROGRAM move2feeder()
	CALL defvars()
	MOVE #close_to_feeder
.END

.PROGRAM break()
	BREAK
.END

.PROGRAM put_tool()

	AUTO REAL lastspeed
	AUTO REAL id

	id = 26

	ATTACH ()
	lastspeed = SPEED(2)
	SPEED 50 ALWAYS
	CALL r._puttool(1, id, index)
	SPEED lastspeed ALWAYS

.END

.PROGRAM get_tool(id)

	ATTACH ()
	CALL r1.gettool(id)
.END

.PROGRAM get_calibtool()

	CALL get_tool(11)

.END

.PROGRAM get_tool26()

	CALL get_tool(26)

.END

.PROGRAM move_to(pose_params[])

	x = pose_params[0]
	y = pose_params[1]
	z = pose_params[2]
	roll = pose_params[3]
	pitch = pose_params[4]
	yaw = pose_params[5]

	MOVE TRANS(x,y,z,roll,pitch,yaw)

.END

.LOCATIONS
#close_to_feeder  -100 -54 185 0 50 0
 insp_approach  -0.053321108 0.997576058 0.044708055 0.996407151 0.050201692 0.068209276
 0.065799527 0.048184421 -0.996668756 558.356018066 -365.489013672 443.312988281
#intermediate  -100 -90 180 0 90 0
 now         0.173607349 0.984660387 0.017450865 0.984810293 -0.173633784 2.38129942E-07
 3.03029432E-03 0.017185749 -0.99984777 -113.364524841 -642.92578125 248.999542236
#orig        0 -90 180 0 90 0
 over_griphome_1  0 -1 0 -1 0 0
 0 0 -1 167.899993896 -243.300003052 600
#over_griphome_2  -55.400001526 -121.900001526 219.600006104 0 90 -90
 test_1      0 -1 0 -1 0 0
 0 0 -1 480 0 500
.END

.REALS
 err_code   1
 i          6
 nparams    7
 p[0]       -213.367004395
 p[1]       -642.809997559
 p[2]       224.964996338
 p[3]       80
 p[4]       179.005004883
 p[5]       -180
 pitch      179.005004883
 roll       80
 status     1
 x          -213.367004395
 y          -642.809997559
 yaw        -180
 z          224.964996338
.END

.DOUBLES
.END

.STRINGS
$func       "move_to"
$input      "move_to:-213.367,-642.810,224.965,80.000,179.005,-180.000"
$ip_vbox    "172.16.0.125"
$tmp        ""
.END

