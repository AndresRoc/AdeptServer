.PROGRAM simplemove()

	ATTACH ()
	CALL defvars()

	MOVE #orig
	BREAK

	MOVE #intermediate
	BREAK

	MOVE #close_to_feeder
	BREAK

	;MOVE #intermediate
	;BREAK

	;MOVE #orig
	;BREAK


.END

.PROGRAM writenfs()

	;AUTO nfslun
	;nfslun = 27


	;ATTACH (lun, 4) "DISK"
	ATTACH (lun, 4) "NFS"
	CALL typeiostat("attaching", lun)

	;$fname = "xc:\oleksandr_vplus\test.dat"

	$fname = "alex_nfs.dat"

	FOPENW (lun) $fname
	CALL typeiostat("opening a file", lun)
	rnd = RANDOM
	WRITE (lun) $TIME(), rnd
	CALL typeiostat("writing attempt", lun)
	TYPE $TIME(), rnd, lun
	FCLOSE (lun)
	DETACH (lun)


.END

.PROGRAM checksig()

	SIGNAL -99, 100
	TYPE SIG(99), SIG(100)
	SIGNAL 99, -100
	TYPE SIG(99), SIG(100)

	TYPE SIG(3001), SIG(3002)
	SIGNAL 3001, -3002

.END

.PROGRAM typeiostat($op_string, lun)

	err_code = IOSTAT(lun)
	TYPE "After ", $op_string, ":", err_code
	IF err_code < 0 THEN
	    TYPE $ERROR(err_code)
	END

.END

.PROGRAM enable_air()

	IF SIG(8) == FALSE THEN
	    SIGNAL 8
	END
.END

.PROGRAM defvars()

	; Transformations and precision points
	SET #orig = #PPOINT(0,-90,180,0,90,0)
	SET #intermediate = #PPOINT(-100,-90,180,0,90,0)
	SET #close_to_feeder = #PPOINT(-100,-54,185,0,50,0)
	SET over_griphome_1 = TRANS(167.9,-243.3,600,0,180,-90)	; to be further refined
	SET #over_griphome_2 = #PPOINT(-55.4,-121.9,219.6,0,90,-90)
	SET test_1 = TRANS(480,0,500,0,180,-90)

	; IP addresses
	$ip_vbox = "172.16.0.125"

.END

.PROGRAM gripperop()

	CALL enable_air()
	CALL gripper_close()

	CALL gripper_open()

	;CALL disable_air()

.END

.PROGRAM disable_air()

	IF SIG(8) THEN
	    SIGNAL -8
	END

.END

.PROGRAM gripper_open()

	CALL enable_air()

	;TYPE SIG(99), SIG(-100)

	;IF SIG(99) AND SIG(-100) THEN
	;SIGNAL -99, 100
	;END

	SIGNAL -99, 100

.END

.PROGRAM gripper_close()

	CALL enable_air()
	;IF SIG(-99) AND SIG(100) THEN
	;SIGNAL 99, -100
	;END

	SIGNAL 99, -100

.END

.PROGRAM tcpcreateclient($ip, port, lun)
	ATTACH (lun, 4) "TCP"
	;CALL typeiostat("establishing TCP client LUN", lun)

	IF IOSTAT(lun) < 0 THEN
	    CALL tcpdestrclient(lun)
	END

	FOPEN (lun, 0) $ip, " /REMOTE_PORT", port, " /BUFFER_SIZE 1024"
	;CALL typeiostat("creating client socket", lun)

	IF IOSTAT(lun) < 0 THEN
	    CALL tcpdestrclient(lun)
	END
.END

.PROGRAM tcpdestrclient(lun)
	FCLOSE (lun)
	DETACH (lun)
	CALL typeiostat("destroying TCP client", lun)
.END

.PROGRAM tcpsend($ip, port, $msg)
	AUTO lun

	ATTACH (lun, 4) "TCP"
	FOPEN (lun, 0) $ip, " /REMOTE_PORT", port, " /BUFFER_SIZE 1024"

	WRITE (lun) $msg

	FCLOSE (lun)
	DETACH (lun)
.END

.PROGRAM tcptestsrv()

	AUTO do_wait, no_wait, handle, lun, repeat_loop

	do_wait = 0
	no_wait = 1

	ATTACH (lun, 4) "TCP"
	CALL typeiostat("establishing TCP server LUN", lun)

	FOPEN (lun, 16) "/LOCAL_PORT 1234 /CLIENTS 10 /BUFFER_SIZE 1024"
	CALL typeiostat("creating server socket", lun)

	repeat_loop = TRUE
	WHILE repeat_loop DO

	    WAIT
	    READ (lun, handle, do_wait) $input
	    status = IOSTAT(lun)
	    CASE status OF
	      VALUE 100:
		TYPE "Connection established"
	      VALUE 1:
		TYPE "Received message ", $input
	      VALUE 101:
		TYPE "Connection closed"
	    END

	    ;TYPE $input

	END


	FCLOSE (lun)
	DETACH (lun)
	CALL typeiostat("closing", lun)
.END

.PROGRAM testclient()
	;lun = -10
	;AUTO lun
	CALL defvars()
	CALL tcpcreateclient($ip_vbox, 8888, lun)
	CALL tcpdestrclient(lun)
.END

.PROGRAM movendrop()

	ATTACH ()

	CALL defvars()
	CALL enable_air()

	MOVE #orig
	BREAK

	MOVE #intermediate
	BREAK

	MOVE #close_to_feeder
	BREAK
	CALL gripper_open()

	MOVE #intermediate
	BREAK

	MOVE #orig
	BREAK

.END

.PROGRAM movehome()
	ATTACH ()
	WAIT.EVENT , 4
	CALL defvars()

	MOVE #intermediate
	BREAK

	MOVE #orig
	BREAK
.END

.PROGRAM move2feeder()
	CALL defvars()

	MOVE #orig
	BREAK

	MOVE #intermediate
	BREAK

	MOVE #close_to_feeder
	BREAK

	diff_z = 104.8
	MOVE HERE:TRANS(0,0,diff_z,0,0,0)
	BREAK

.END

.PROGRAM pick()
	CALL defvars()

	MOVE #orig
	BREAK

	MOVE #intermediate
	BREAK

	MOVE #close_to_feeder
	BREAK

	HERE c2f_pose
	DECOMPOSE c2f[] = c2f_pose
	target_z = 250.911
	MOVE TRANS(c2f[0],c2f[1],target_z,c2f[3],c2f[4],c2f[5])
	BREAK
	WAIT.EVENT , 2

	CALL gripper_close()

	;CALL movehome()

.END

.PROGRAM movenwrite()
	ATTACH ()
	CALL defvars()

	ATTACH (lun, 4) "NFS"
	CALL typeiostat("attaching", lun)
	$fname = "poses.csv"
	FOPENA (lun) $fname
	CALL typeiostat("opening a file", lun)

	FOR i = 1 TO 3 STEP 1
	    TYPE i

	    MOVE #orig
	    BREAK

	    MOVE #intermediate
	    BREAK

	    MOVE #close_to_feeder
	    BREAK

	    HERE c2f_pose
	    DECOMPOSE c2f[] = c2f_pose
	    target_z = 250.911
	    MOVE TRANS(c2f[0],c2f[1],target_z,c2f[3],c2f[4],c2f[5])
	    BREAK
	    WAIT.EVENT , 2

	    HERE pose
	    DECOMPOSE pose_arr[] = pose
	    CALL writecsvline(lun, pose_arr[])

	    ;n = LAST(pose_arr[])
	    ;FOR j = 0 TO n-1
	    ;    WRITE (lun) pose_arr[j], /S
	    ;    WRITE (lun) ",", /S
	    ;END
	    ;WRITE (lun) pose_arr[n]

	    TYPE "Next...", i+1

	    MOVE #intermediate
	    BREAK

	    MOVE #orig
	    BREAK


	END

	FCLOSE (lun)
	DETACH (lun)

.END

.PROGRAM writecsvline(lun, arr[])
	AUTO i
	n = LAST(arr[])
	$res = ""
	FOR i = 0 TO n-1
	    $res = $res+$ENCODE(arr[i])+","
	END
	$res = $res+$ENCODE(arr[i])+$CHR(9)

	WRITE (lun) $res
.END

.PROGRAM pickndrop()

	ATTACH ()
	WAIT.EVENT , 2
	CALL pick()
	CALL movehome()
	WAIT.EVENT , 2
	CALL movendrop()

.END

.PROGRAM rosposecl()

	CALL defvars()
	CALL tcpcreateclient($ip_vbox, 8888, lun)

	HERE pose
	DECOMPOSE pose_arr[] = pose

	CALL writecsvline(lun, pose_arr[])

	CALL tcpdestrclient(lun)

.END

.PROGRAM testcompound()

	$s = ""
	TYPE $s
	$s = $s+$ENCODE(23)
	TYPE $s


.END

.LOCATIONS
 c2f_pose    0.173644572 0.984654725 0.017393846 0.984803736 -0.173670843 -4.03297292E-07
 3.0204067E-03 0.017129594 -0.999848664 -113.349739075 -642.838928223 353.409118652
#close_to_feeder  -100 -54 185 0 50 0
#intermediate  -100 -90 180 0 90 0
#orig        0 -90 180 0 90 0
 over_griphome_1  0 -1 0 -1 0 0
 0 0 -1 167.899993896 -243.300003052 600
#over_griphome_2  -55.400001526 -121.900001526 219.600006104 0 90 -90
 pose        0.173644662 0.984654784 0.017393062 0.984803736 -0.173670933 -4.04834225E-07
 3.02027096E-03 0.017128823 -0.999848723 -113.349746704 -642.838989258 250.910873413
 test_1      0 -1 0 -1 0 0
 0 0 -1 480 0 500
.END

.REALS
 c2f[0]     -113.349739075
 c2f[1]     -642.838928223
 c2f[2]     353.409118652
 c2f[3]     80.000007629
 c2f[4]     179.003356934
 c2f[5]     -179.998672485
 err_code   1
 i          4
 lun        31
 n          5
 pose_arr[0] -113.349746704
 pose_arr[1] -642.838989258
 pose_arr[2] 250.910873413
 pose_arr[3] 80.000007629
 pose_arr[4] 179.00340271
 pose_arr[5] -179.998672485
 target_z   250.910995483
.END

.DOUBLES
.END

.STRINGS
$fname      "poses.csv"
$ip_vbox    "172.16.0.125"
$res        " -113.3497, -642.839, 250.9109, 80.00001, 179.0034, -179.9987^i"
$s          " 23"
.END

